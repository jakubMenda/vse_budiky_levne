*** Settings ***
Library    Selenium2Library    
Resource    ../Objects/BudikyObjects.txt
Resource    ../Settings/BudikySettings.txt

*** Keywords ***
Go To the Shopping Cart
    Wait Until Page Contains Element    ${oneProductCartIcon} 
    Page Should Contain Element    ${cartIconClassName}    
    Click Element    ${cartIconClassName}
    Wait Until Page Contains Element    ${cartContainerClassName} 
    Wait Until Page Contains Element    ${shoppingCartItemClassName}  
    
Navigate Via Link
    [Arguments]    ${LinkElement}    ${expectedUrl}
    Wait Until Page Contains Element    ${linkElement}
    Click Element    ${linkElement}    
    Location Should Be    ${expectedUrl}
    
Go To Product Detail
    Wait Until Page Contains Element    ${catalogueItemClassName}
    Click Element    ${catalogueItemClassName}    
    Wait Until Page Contains Element    ${productDetailHeadingClassName}
	Page Should Contain Element    ${productDetailGalleryClassName}    
	Page Should Contain Element    ${productDetailDescClassName}   
	Page Should Contain Element    ${productDetailSubmitButtonClassName}   
	
Add the Product To the Shopping Cart
    Go To Product Detail
    Click Element    ${productDetailSubmitButtonClassName}   

Add Product And Go To Cart
    Add the Product To the Shopping Cart 
    Go To the Shopping Cart

Click On Change Quantity Icon
    [Arguments]    ${button}    ${input}    ${icon}
    Click Element    ${button}
    Reload Page
    Wait Until Page Contains Element    ${input}
    Wait Until Page Contains Element    ${icon} 
    
Go To Checkout
    Add Product And Go To Cart
    Click Element    ${checkoutLink}
    Wait Until Page Contains Element    ${checkoutContainerClassName}

Select Company Shopping
    Page Should Contain Checkbox    ${buyAsCompanyCheckbox}    
    Select Checkbox    ${buyAsCompanyCheckbox}
    Wait Until Page Contains Element    ${billingCompanyInput}
    
Submit the Address
    Input Text    ${billingFirstNameInput}    ${customerName}
    Input Text    ${billingStreetInput}    ${customerStreet}
    Input Text    ${billingCityInput}    ${customerCity}
    Input Text    ${billingZipInput}    ${customerZipCode}
    Click Element    ${billingCountrySelect}
    Select From List By Label    ${billingCountrySelect}    ${customerCountry}
    Input Text    ${emailInput}    ${customerEmail}
    Input Text    ${phoneInput}    ${customerPhone}
    Click Button    ${submitNextButton}
    Wait Until Page Contains Element    ${checkoutShippingContainer}     
    
Go To the Payment Options
    Submit the Address
    Select Radio Button    ${shippingMethodRadioGroup}    ${personalCollectionRadioValue}
    Wait Until Page Contains Element    ${shopAddressClassName}
    Wait Until Element Is Visible    ${submitShipping}  
    Click Element    ${submitShipping}
    Wait Until Page Contains Element    ${checkoutPaymentContainer}
    
Select Payment Method
    [Arguments]    ${radioValue}    ${textExpected}
    Go To The Payment Options
    Sleep    2
    Select Radio Button    ${paymentMethodRadioGroup}    ${radioValue}
    Wait Until Element Is Visible    ${submitPayment}
    Click Element    ${submitPayment}
    Wait Until Page Contains Element    ${checkoutConfirmContainer}
    Wait Until Page Contains    ${textExpected}
    